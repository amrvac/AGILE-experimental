#:include "../mod_gpu_directives.fpp"
module mod_errest
  use mod_comm_lib, only: mpistop
  implicit none
  private

  public :: errest, forcedrefine_grid_io

contains

  !> Do all local error estimation which determines (de)refinement
  subroutine errest
    use mod_forest, only: refine, buffer, coarsen
    use mod_global_parameters

    integer :: igrid, iigrid

    ! Locals inlined from lohner_grid (see comment below for why this is
    ! inlined rather than a called GPU_ROUTINE_VECTOR subroutine)
    integer                            :: iflag, idims1, idims2, level
    integer                            :: ix1, ix2, ix3
    double precision                   :: threshold, error, numerator, denominator
    logical                            :: refineflag, coarsenflag
    double precision, parameter        :: epsilon=1.0d-6

    if (igridstail==0) return

    select case (refine_criterion)
    case (1)
       ! all refinement solely based on user routine usr_refine_grid
    case (3)
       ! Error estimation is based on Lohner's scheme.
       ! Inlined version of lohner_igrid subroutine, because a vector-parallelism
       ! subroutine called from a gang loop does not work with OpenMP.
       ! Note: the same could happen to a user-defined refinement, but this has not yet been tested.
       ${GPU_PARALLEL_LOOP_GANG("private(igrid, level, threshold, refineflag, coarsenflag)")}$
       do iigrid=1,igridstail; igrid=igrids(iigrid);

          level       = node(plevel_,igrid)
          threshold   = refine_threshold(level)

          refineflag  = .false.
          coarsenflag = .true.
          ${GPU_LOOP_VECTOR("collapse(3) reduction(.or.:refineflag) reduction(.and.:coarsenflag) private(ix1,ix2,ix3,error,iflag,idims1,idims2,numerator,denominator)")}$
          do ix3 = ixMlo3, ixMhi3
             do ix2 = ixMlo2, ixMhi2
                do ix1 = ixMlo1, ixMhi1

                   error = zero
                   ${GPU_LOOP_SEQ()}$
                   do iflag = 1, nw
                      if(w_refine_weight(iflag)==0.d0) cycle

                      numerator   = zero
                      denominator = zero
                      ${GPU_LOOP_SEQ()}$
                      do idims1 = 1, ndim
                         do idims2 = 1, ndim

                            numerator = numerator + &
                                 ( &
                                 ( bg(1)%w(ix1+kr(1,idims2)+kr(1,idims1), &
                                 ix2+kr(2,idims2)+kr(2,idims1), &
                                 ix3+kr(3,idims2)+kr(3,idims1), iflag, igrid)    &
                                 - bg(1)%w(ix1-kr(1,idims2)+kr(1,idims1), &
                                 ix2-kr(2,idims2)+kr(2,idims1), &
                                 ix3-kr(3,idims2)+kr(3,idims1), iflag, igrid) )  &
                                 - &
                                 ( bg(1)%w(ix1+kr(1,idims2)-kr(1,idims1), &
                                 ix2+kr(2,idims2)-kr(2,idims1), &
                                 ix3+kr(3,idims2)-kr(3,idims1), iflag, igrid)    &
                                 - bg(1)%w(ix1-kr(1,idims2)-kr(1,idims1), &
                                 ix2-kr(2,idims2)-kr(2,idims1), &
                                 ix3-kr(3,idims2)-kr(3,idims1), iflag, igrid) )  &
                                 )**2

                            denominator = denominator + &
                                 ( &
                                 abs( &
                                 bg(1)%w(ix1+2*kr(1,idims1), ix2+2*kr(2,idims1), ix3+2*kr(3,idims1), iflag, igrid) &
                                 - bg(1)%w(ix1, ix2, ix3, iflag, igrid) &
                                 ) &
                                 + abs( &
                                 bg(1)%w(ix1, ix2, ix3, iflag, igrid) &
                                 - bg(1)%w(ix1-2*kr(1,idims1), ix2-2*kr(2,idims1), ix3-2*kr(3,idims1), iflag, igrid) &
                                 ) &
                                 + amr_wavefilter(level) * ( &
                                 ( abs( bg(1)%w(ix1+kr(1,idims1)+kr(1,idims2), &
                                 ix2+kr(2,idims1)+kr(2,idims2), &
                                 ix3+kr(3,idims1)+kr(3,idims2), iflag, igrid) )   &
                                 + abs( bg(1)%w(ix1-kr(1,idims1)+kr(1,idims2), &
                                 ix2-kr(2,idims1)+kr(2,idims2), ix3-kr(3,idims1)+kr(3,idims2), iflag, igrid) ) ) &
                                 + &
                                 ( abs( bg(1)%w(ix1+kr(1,idims1)-kr(1,idims2), &
                                 ix2+kr(2,idims1)-kr(2,idims2), &
                                 ix3+kr(3,idims1)-kr(3,idims2), iflag, igrid) )   &
                                 + abs( bg(1)%w(ix1-kr(1,idims1)-kr(1,idims2), &
                                 ix2-kr(2,idims1)-kr(2,idims2), &
                                 ix3-kr(3,idims1)-kr(3,idims2), iflag, igrid) ) ) &
                                 ) &
                                 )**2
                         end do
                      end do

                      error = error + w_refine_weight(iflag) * sqrt( numerator / max( denominator, epsilon ) )

                   end do

                   if (error > threshold) then
                      refineflag = .true.
                   end if
                   if (error > derefine_ratio(level) * threshold) then
                      coarsenflag = .false.
                   end if

                end do
             end do
          end do

          if (refineflag .and. level < refine_max_level) refine(igrid,mype)=.true.
          if (coarsenflag .and. level > 1) coarsen(igrid,mype)=.true.

       end do
    case default
       call mpistop("Unknown error estimator")
    end select

    if ( refine_usr ) then
       ${GPU_PARALLEL_LOOP_GANG("private(igrid)")}$
       do iigrid=1,igridstail; igrid=igrids(iigrid);
          call forcedrefine_grid(igrid)
       end do
    end if

    ${GPU_UPDATE_HOST('refine, coarsen')}$

  end subroutine errest


  subroutine forcedrefine_grid(igrid)
    ${GPU_ROUTINE_VECTOR()}$
    #:if defined('REFINE_USR')
    use mod_usr, only: usr_refine_grid
    #:endif
    use mod_forest, only: coarsen, refine, buffer
    use mod_global_parameters

    integer, intent(in) :: igrid

    integer :: level
    integer :: my_refine, my_coarsen
    double precision :: qt

    level=node(plevel_,igrid)

    ! initialize to 0
    my_refine   = 0
    my_coarsen  = 0

    if (time_advance) then
       qt=global_time+dt
    else
       qt=global_time
    end if

#:if defined('REFINE_USR')
    call usr_refine_grid(igrid,level,ixGlo1,ixGlo2,ixGlo3,ixGhi1,ixGhi2, &
         ixGhi3,ixMlo1,ixMlo2,ixMlo3,ixMhi1,ixMhi2,ixMhi3,qt, &
         bg(1)%w(:,:,:,:, igrid), ps(igrid)%x, &
         my_refine,my_coarsen)
#:endif

    if (my_coarsen==1) then
       if (level>1) then
          refine(igrid,mype)=.false.
          coarsen(igrid,mype)=.true.
       else
          refine(igrid,mype)=.false.
          coarsen(igrid,mype)=.false.
       end if
    end if

    if (my_coarsen==-1)then
       coarsen(igrid,mype)=.false.
    end if

    if (my_refine==1) then
       if (level<refine_max_level) then
          refine(igrid,mype)=.true.
          coarsen(igrid,mype)=.false.
       else
          refine(igrid,mype)=.false.
          coarsen(igrid,mype)=.false.
       end if
    end if

    if (my_refine==-1) then
      refine(igrid,mype)=.false.
    end if

  end subroutine forcedrefine_grid

  subroutine forcedrefine_grid_io(igrid,w)
    use mod_forest, only: coarsen, refine
    use mod_global_parameters

    integer, intent(in)          :: igrid
    double precision, intent(in) :: w(ixGlo1:ixGhi1,ixGlo2:ixGhi2,&
       ixGlo3:ixGhi3,nw)

    integer                   :: level, my_levmin, my_levmax
    logical, dimension(ixGlo1:ixGhi1,ixGlo2:ixGhi2,&
       ixGlo3:ixGhi3) :: refineflag

    level=node(plevel_,igrid)

    if (level_io > 0) then
       my_levmin = level_io
       my_levmax = level_io
    else
       my_levmin = max(1,level_io_min)
       my_levmax = min(refine_max_level,level_io_max)
    end if

    if (level>my_levmax) then
      refine(igrid,mype)=.false.
      coarsen(igrid,mype)=.true.
    elseif (level<my_levmin) then
      refine(igrid,mype)=.true.
      coarsen(igrid,mype)=.false.
    end if

    if (level==my_levmin .or. level==my_levmax) then
      refine(igrid,mype)=.false.
      coarsen(igrid,mype)=.false.
    end if

    if(refine(igrid,mype).and.level>=refine_max_level)refine(igrid,&
       mype)=.false.
    if(coarsen(igrid,mype).and.level<=1)coarsen(igrid,mype)=.false.

  end subroutine forcedrefine_grid_io

end module mod_errest
